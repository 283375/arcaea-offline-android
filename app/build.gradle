plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'

    id 'kotlinx-serialization'
}

secrets {
    defaultPropertiesFileName = 'local.defaults.properties'
}

def properties = new Properties()
properties.load(project.rootProject.file("local.defaults.properties").newDataInputStream())
properties.load(project.rootProject.file("local.properties").newDataInputStream())

// automatic version generating from https://stackoverflow.com/a/24121734/16484891
// CC BY-SA 3.0
def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    } catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdoutStream = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdoutStream
        }
        def stdout = stdoutStream.toString().trim()
        return stdout
    } catch (ignored) {
        return null
    }
}

android {
    namespace 'xyz.sevive.arcaeaoffline'
    compileSdk 34

    defaultConfig {
        applicationId 'xyz.sevive.arcaeaoffline'
        minSdk 21
        targetSdk 34
        versionCode getVersionCode()
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["app_stable"]

    productFlavors {
        stable {
            dimension "app_stable"
        }
        unstable {
            dimension "app_stable"
            applicationIdSuffix ".unstable"
            versionNameSuffix "-unstable"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.4'
    }
    packaging {
        resources.excludes.add('/META-INF/{AL2.0,LGPL2.1}')
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"

    def room_version = "2.6.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    implementation "androidx.datastore:datastore:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.activity:activity-compose:1.8.0'
    implementation platform('androidx.compose:compose-bom:2023.03.00')

    def compose_ui_version = "1.5.4"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-graphics:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_ui_version"

    def compose_material3_version = "1.1.2"
    implementation "androidx.compose.material3:material3:$compose_material3_version"
    implementation "androidx.compose.material3:material3-window-size-class:$compose_material3_version"

    implementation "androidx.window:window:1.1.0"

    implementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.2"

    def nav_version = "2.7.5"
    implementation "androidx.navigation:navigation-compose:$nav_version"

    implementation 'com.google.android.material:material:1.10.0'

    implementation 'com.github.requery:sqlite-android:3.43.0'

    def acra_version = "5.11.3"
    implementation "ch.acra:acra-http:$acra_version"
    implementation "ch.acra:acra-dialog:$acra_version"

    def richtext_version = "0.17.0"
    implementation "com.halilibo.compose-richtext:richtext-commonmark:${richtext_version}"
    implementation "com.halilibo.compose-richtext:richtext-ui-material3:${richtext_version}"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform("androidx.compose:compose-bom:2023.03.00")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    implementation project(path: ':opencv')
}
